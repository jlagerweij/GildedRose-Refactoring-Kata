plugins {
    id("java")
    id("pmd")
    id("info.solidsoft.pitest") version "1.15.0"
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.10.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.10.2")
}

group = "com.gildedrose"
version = "0.0.1-SNAPSHOT"

java {
    sourceCompatibility = "1.8"
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-Xlint:-options")
}

tasks.register("texttest", JavaExec) {
    mainClass = "com.gildedrose.GoldIntegrationTest"
    classpath = sourceSets.test.runtimeClasspath
    args "30"
}

pmd {
    consoleOutput = true
    ruleSetFiles = files("$rootDir/src/test/resources/pmd-exclusions.xml")
}

pitest {
    failWhenNoMutations = false
    junit5PluginVersion = "1.2.1"
    pitestVersion = "1.15.3"
    targetClasses = ["com.gildedrose.*"]
    useClasspathFile = true
    timeoutFactor = BigDecimal.valueOf(2L)
    timeoutConstInMillis = 30000
    timestampedReports = false
    excludedMethods = ["toString"]
    threads = 1
    maxSurviving = 0
}
